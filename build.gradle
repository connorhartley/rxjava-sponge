buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'idea'
    id 'eclipse'
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'org.spongepowered.plugin' version '0.8.2'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.hierynomus.license'

defaultTasks 'licenseFormat', 'build'

group = "com.ichorpowered"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.spongepowered.org/maven' }
}

dependencies {
    compileOnly 'org.spongepowered:spongeapi:7.1.0-SNAPSHOT'

    compile 'io.reactivex:rxjava:1.3.8'
}

blossom {
    def location = 'src/main/java/com/ichorpowered/rxs/RxsPlugin.java'

    replaceToken '@id@', 'rxjava-sponge', location
    replaceToken '@name@', 'ReactiveX Sponge', location
    replaceToken '@version@', version, location
    replaceToken '@description@', 'A ReactiveX wrapper for Sponge.', location
}

shadowJar {
    dependencies {
        include dependency('io.reactivex:rxjava')
    }
}

apply from: project.file("gradle/implementation.gradle")
apply from: project.file("gradle/publish.gradle")

build.dependsOn(javadocJar)
build.dependsOn(sourceJar)
build.dependsOn(jar)
build.dependsOn(shadowJar)